import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:${mavenPublishVersion}"
    }
}

plugins {
    id 'io.freefair.code-generator' version "${codeGenVersion}"
}

apply from: "${rootProject.rootDir}/gradle/ghPages.gradle"
apply from: "${rootProject.rootDir}/gradle/deploy.gradle"

generateTestCode.enabled = false

dependencies {
    api deps.okhttpLoggingInterceptor

    codeGenerator project(':template-generator')

    implementation deps.gson
    implementation deps.pusher
    implementation deps.okhttp
    implementation deps.okhttpUrlConnection
    implementation deps.retrofit
    implementation deps.retrofitGson
    implementation deps.log4j2Api
    implementation deps.log4j2Core

    testImplementation(
            deps.junitPlatform,
            deps.junitJupiter,
            deps.mockito,
            deps.mockitoInline,
            deps.mockitoJupiter,
            deps.retrofitMock,
            deps.awaitility,
    )
    testImplementation(deps.mockWebServer) {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
}

task processSource(type: Sync) {
    from sourceSets.main.java
    inputs.property 'version', version
    filter(ReplaceTokens, tokens: ["version": version])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

test {
    useJUnitPlatform()
    ignoreFailures true
    systemProperty 'junit.jupiter.execution.timeout.mode', 'disabled_on_debug'
}
